buildscript {
    ext.gretty_version = '2.0.0'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
        classpath "org.akhikhl.gretty:gretty:$gretty_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'

group 'net.tkhamez.everoute'
version '0.1.0'
mainClassName = "io.ktor.server.netty.EngineMain"
webAppDirName = 'webapp'

// needs to be after "mainClassName = ..."
apply plugin: 'com.github.johnrengelman.shadow'

gretty {
    contextPath = '/'
    logbackConfigFile = 'resources/logback.xml'
}

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://kotlin.bintray.com/ktor' }
    maven { url 'https://kotlin.bintray.com/kotlinx' }
}

configurations {
    kotlinc
    //scripts
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "io.ktor:ktor-locations:$ktor_version"
    implementation "io.ktor:ktor-server-servlet:$ktor_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-server-host-common:$ktor_version"
    implementation "io.ktor:ktor-server-sessions:$ktor_version"
    implementation "io.ktor:ktor-auth:$ktor_version"
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-core-jvm:$ktor_version"
    implementation "io.ktor:ktor-client-apache:$ktor_version"
    implementation "io.ktor:ktor-client-json-jvm:$ktor_version"
    implementation "io.ktor:ktor-client-gson:$ktor_version"
    implementation "io.ktor:ktor-client-logging-jvm:$ktor_version"

    compile "com.auth0:java-jwt:3.10.3"
    compile "com.auth0:jwks-rsa:0.13.0"

    compile 'org.litote.kmongo:kmongo:4.0.3'
    //compile 'org.litote.kmongo:kmongo-async:4.0.3'
    //compile 'org.litote.kmongo:kmongo-coroutine:4.0.3'
    //compile 'org.litote.kmongo:kmongo-rxjava2:4.0.3'

    testImplementation "io.ktor:ktor-server-tests:$ktor_version"

    implementation "org.jetbrains.kotlin:kotlin-script-runtime:$kotlin_version"
    //kotlinc "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    //kotlinc "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    kotlinc "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlin_version"
    kotlinc "org.jetbrains.kotlin:kotlin-scripting-compiler-embeddable:$kotlin_version"
    kotlinc "io.ktor:ktor-client-gson:$ktor_version"
    //scripts "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

def kotlinHome = file("$buildDir/kotlin")

task prepareKotlinHome(type: Copy) {
    from(configurations.kotlinc)
    into("$kotlinHome/lib")
    rename {
        if (it.indexOf("kotlin-") != -1) {
            // strip the version suffix to satisfy the compiler
            (it - "-${kotlin_version}.jar") + ".jar"
        }
    }
}

task buildGraph(type: JavaExec) {
    dependsOn prepareKotlinHome
    dependsOn classes
    main = "org.jetbrains.kotlin.cli.jvm.K2JVMCompiler"
    classpath = configurations.kotlinc
    args(
        "-kotlin-home", kotlinHome,
        //"-classpath", configurations.scripts.asPath,
        "-classpath", "$buildDir/kotlin/lib/gson-2.8.6.jar:$buildDir/classes/kotlin/main",
        "-script", "src/scripts/buildGraph.kts"
    )
}

// This task will generate your fat JAR and put it in the ./build/libs/ directory
shadowJar {
    //dependsOn buildGraph -> executes task, but too late? -> graph.json is not included in jar file
    manifest {
        attributes 'Main-Class': "io.ktor.server.netty.EngineMain"
    }
}

war.dependsOn buildGraph

task stage {
    dependsOn buildGraph
    dependsOn installDist
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}
